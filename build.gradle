plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "re_dimdoors" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

    runs {
        configureEach {
            vmArg "-XX:+AllowEnhancedClassRedefinition"
            vmArg "-XX:+IgnoreUnrecognizedVMOptions"
        }

        server {
            runDir "run/server"
        }

        gametestServer {
            server()
            name "Headlesss GameTests"
            vmArg "-Dfabric-api.gametest"
            vmArg "-Dfabric-api.gametest.report-file=${project.buildDir}/junit.xml"
            runDir "run/gametest"
        }
    }

}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven { url = "https://maven.shedaniel.me/" } // Cloth Config
    maven { url = "https://maven.architectury.dev/" } // Architectury API
    maven { url = "https://jitpack.io" } // poly2tri
    maven { url = "https://api.modrinth.com/maven" } // LazyDFU
    maven { url = "https://maven.terraformersmc.com/" } // Mod Menu, EMI
}

configurations.configureEach {
    resolutionStrategy {
        force("net.fabricmc:fabric-loader:$loader_version")
    }
}

def includeCompile(DependencyHandler deps, entry) {
    project.dependencies {
        deps.modApi(entry)
        deps.include(entry)
    }
}

def devEnv(DependencyHandler deps) {
    deps.modLocalRuntime "maven.modrinth:lazydfu:${lazy_dfu_version}"
    deps.modLocalRuntime "com.terraformersmc:modmenu:${modmenu_version}"
    deps.modLocalRuntime "dev.emi:emi-fabric:${emi_version}+${minecraft_version}"
    deps.modLocalRuntime "mezz.jei:jei-${minecraft_version}-fabric:${jei_version}"
}

def setup(DependencyHandler deps) {
    deps.minecraft "com.mojang:minecraft:${project.minecraft_version}"
    deps.mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
    }
    deps.modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

def required(DependencyHandler deps) {
    deps.modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    deps.modImplementation "maven.modrinth:dimensionaldoors:${rootProject.dimdoors_version}-fabric"

    deps.modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"
    deps.modApi "me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config}"

    includeCompile(deps, "com.flowpowered:flow-math:1.0.3")
    includeCompile(deps, "org.jgrapht:jgrapht-core:1.1.0")
    includeCompile(deps, "com.github.DimensionalDevelopment:poly2tri.java:0.1.1")
}

dependencies {
    setup(dependencies)
    devEnv(dependencies)
    required(dependencies)
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    inputs.property "dimdoors_version", project.dimdoors_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "dimdoors_version": project.dimdoors_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
